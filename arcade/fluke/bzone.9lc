! Battlezone Test Script
! Douglas Gauck Oct 5 2016
! version 0.5
! Same signatures for v1 and v2 ROMs?
! VGEN test may not work 
! No DIP test

SETUP
   INCLUDE "6502.POD"
   POD 6502
   TRAP ACTIVE FORCE LINE NO

ADDRESS SPACE INFORMATION
   RAM @ 0000-03FF	! PROGRAM RAM - 2114 SRAM
   RAM @ 2000-27FF	! VECTOR RAM -2114 SRAM

PROGRAM 0

   DPY-BATTLEZONE TEST PROGRAM
   DPY-+-PRESS CONT
   STOP
0: LABEL 0
   DPY-1=RAM 2=ROM 3=SND 4=IO 
   DPY-+ 5=VG 6=ADEC /1
   IF REG1 = 1 GOTO 1
   IF REG1 = 2 GOTO 2
   IF REG1 = 3 GOTO 3
   IF REG1 = 4 GOTO 4
   IF REG1 = 5 GOTO 5
   IF REG1 = 6 GOTO 6
   GOTO 0
1: LABEL 1
   EXECUTE PROGRAM 10         !Ram Test
   GOTO 0
2: LABEL 2
   EXECUTE PROGRAM 20		!ROM Test
   GOTO 0
3: LABEL 3
   EXECUTE PROGRAM 30		!Sound Test
   GOTO 0
4: LABEL 4
   EXECUTE PROGRAM 40		!I/O Test
   GOTO 0
5: LABEL 5
   EXECUTE PROGRAM 50		!Vector Graphics Test
   GOTO 0
6: LABEL 6
   EXECUTE PROGRAM 60		!Address Decoders Test
   GOTO 0
7: LABEL 7
   EXECUTE PROGRAM 70		!DIP switch test
   GOTO 0

PROGRAM 10	!RAM

   DPY-TESTING PROGRAM RAM @ H2 J2 #
   RAM SHORT @ 0000-03FF

   DPY-TESTING VECTOR RAM @ A1 A2 #
   RAM SHORT @ 2000-23FF
   
   DPY-TESTING VECTOR RAM @ B1 B2 #
   RAM SHORT @ 2400-27FF

   DPY-TESTING VECTOR RAM @ C1 C2 #
   RAM SHORT @ 2800-2BFF
   
   DPY-TESTING VECTOR RAM @ D1 D2 #
   RAM SHORT @ 2C00-2FFF

   DPY-RAM TESTING COMPLETE #


PROGRAM 12   128 BYTES		!BUILD DATA FOR PGM 60

   DPY-ENTER ADDR, PROBE /1
   REG8 = REG1
   READ PROBE
   REG1 = 40
1: LABEL 1
   WRITE @ REG8 = FF
   DEC REG1
   IF REG1 > 0 GOTO 1
   READ PROBE
   REG0 = REG0 AND 7F
   IF REG0 = 40 GOTO 2
   DPY-INCORRECT WAS $0 NOT 16
   GOTO 3
2: LABEL 2
   DPY-COUNT CORRECT $0
3: LABEL 3


PROGRAM 13

   DPY-ENTER HEX ADDRESS /1
   READ @ REG1
   DPY ADDR $1 HAS $E
   STOP

PROGRAM 14

   DPY-ENTER HEX ADDRESS /1
   DPY-ENTER VALUE /2
   WRITE @ REG1 = REG2
   STOP


PROGRAM 20	!ROM

0: LABEL 0
   DPY-ROM VERSION 1 OR 2 ? /1
   IF REG1 = 1 GOTO 1
   IF REG1 = 2 GOTO 2
   GOTO 0

1: LABEL 1
   DPY-TESTING VECTOR ROM 22 @ B//C3 #
   ROM TEST @ 3000-37FF SIG 6628

   DPY-TESTING VECTOR ROM 21 @ A3 #
   ROM TEST @ 3800-3FFF SIG F6EA

   DPY-TESTING PROGRAM ROM 14 @ E1 #
   ROM TEST @ 5000-57FF SIG B9B1

   DPY-TESTING PROGRAM ROM 13 @ H1 #
   ROM TEST @ 5800-5FFF SIG 62A4

   DPY-TESTING PROGRAM ROM 12 @ J1 #
   ROM TEST @ 6000-67FF SIG E68A

   DPY-TESTING PROGRAM ROM 11 @ K1 #
   ROM TEST @ 6800-6FFF SIG ECB0

   DPY-TESTING PROGRAM ROM 10 @ L//M1 #
   ROM TEST @ 7000-77FF SIG 5D24

   DPY-TESTING PROGRAM ROM 9 @ N1 #
   ROM TEST @ 7800-7FFF SIG 4CCE

   GOTO F

2: LABEL 2
   DPY-TESTING VECTOR ROM 22 @ A3
   ROM TEST @ 3000-37FF SIG 6628

   DPY-TESTING VECTOR ROM 21 @ B//C3 #
   ROM TEST @ 3800-3FFF SIG F6EA

   DPY-TESTING PROGRAM ROM 14 @ E1 #
   ROM TEST @ 5000-57FF SIG D4F8

   DPY-TESTING PROGRAM ROM 13 @ H1 #
   ROM TEST @ 5800-5FFF SIG 62A4

   DPY-TESTING PROGRAM ROM 12 @ J1 #
   ROM TEST @ 6000-67FF SIG E68A

   DPY-TESTING PROGRAM ROM 11 @ K1 #
   ROM TEST @ 6800-6FFF SIG ECB0

   DPY-TESTING PROGRAM ROM 10 @ L//M1 #
   ROM TEST @ 7000-77FF SIG 5D24

   DPY-TESTING PROGRAM ROM 09 @ N1 #
   ROM TEST @ 7800-7FFF SIG 4CCE

F: LABEL F
   DPY-ROM TEST COMPLETE #
   STOP

PROGRAM 30   250 BYTES

   DPY-SHELL SND. CONT=NEXT%1#
   REG1 = 40
0: LABEL 0
   WRITE @ 1820 = 06
   IF 40 > REG1 GOTO 0

   DPY-EXPLOSION SND. CONT=NEXT%1#
   REG1 = 40
1: LABEL 1
   WRITE @ 1840 = 03
   IF 40 > REG1 GOTO 1

   DPY-ENGINE REV SND. CONT=NEXT%1#
   REG1 = 40
2: LABEL 2
   WRITE @ 1860 = 20
   IF 40 > REG1 GOTO 2

   DPY-ENGINE RUN SND. CONT=NEXT%1#
   REG1 = 40
3: LABEL 3
   WRITE @ 1860 = 50
   IF 40 > REG1 GOTO 3

   DPY-OTHER SOUNDS THRU POKEY
   STOP

PROGRAM 40	! I/O TEST
   DPY-COMING SOON
   STOP

PROGRAM 50   267 BYTES

   DPY-CONNECT SCOPE,XY MODE. CONT
   STOP
   WRITE @ 2000 = FF
   WRITE @ 2001 = A3
   WRITE @ 2002 = 00
   WRITE @ 2003 = 02
   WRITE @ 2004 = FF
   WRITE @ 2005 = 97
   WRITE @ 2006 = 00
   WRITE @ 2007 = 90
   WRITE @ 2008 = 00
   WRITE @ 2009 = A2
   WRITE @ 200A = 00
   WRITE @ 200B = 00
   WRITE @ 200C = 00
   WRITE @ 200D = 90
   WRITE @ 200E = FF
   WRITE @ 200F = 33
   WRITE @ 2010 = 00
   WRITE @ 2011 = E0
   DPY-CONFIRM PLUS PATTERN. CONT#
   DPY-+=EXIT%1#
0: LABEL 0
   WRITE @ 3000 = 00
   IF REG1 = 25 GOTO 2
   GOTO 0
2: LABEL 2
   DPY-##

PROGRAM 60   833 BYTES

! REG8=CONTROL:  0 TO WRITE, 1 TO READ
! REG9=ADDRESS
! REGA=VALUE TO WRITE
! REGB=DESIRED PULSE COUNT


   SYNC ADDRESS
   DPY-PLACE PROBE ON CHIP-PIN#
   STOP

   REG8 = 1
   REGB = 40

   DPY-ROM0 L2-6#
   STOP
   REG9 = 5000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-ROM1 L2-8#
   STOP
   REG9 = 5800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-ROM2 M2-12#
   STOP
   REG9 = 6000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-ROM3 M2-11#
   STOP
   REG9 = 6800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-ROM4 M2-10#
   STOP
   REG9 = 7000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-ROM5 M2-9#
   STOP
   REG9 = 7800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-PMEMH M2-7#
   STOP
   REG9 = 6000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-PMEML M2-6#
   STOP
   REG9 = 5000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-VMEM M2-5#
   STOP
   REG9 = 2000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-RAM C4-4.#
   STOP
   REG9 = 0000
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-INPUTS C4-5#
   STOP
   REG9 = 0800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-OUTPUTS C4-6#
   STOP
   REG9 = 1000
   REG8 = 0
   REGA = FF
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-I/O C4-7#
   STOP
   REG9 = 1800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-SINP K5-4#
   STOP
   REG8 = 1
   REG9 = 0800
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-OPT0 K5-5#
   STOP
   REG9 = 0A00
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-OPT1 K5-6#
   STOP
   REG9 = 0C00
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-VGRST K5-9#
   STOP
   REG9 = 1600
   REG8 = 0
   REGA = FF
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-WDCLR K5-10#
   STOP
   REG9 = 1400
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-VGGO K5-11#
   STOP
   REG9 = 1200
DPY-PROBING...#
   EXECUTE PROGRAM 65
   DPY-COIN K5-12#
   STOP
   REG9 = 1000
DPY-PROBING...#
   EXECUTE PROGRAM 65

   DPY-TEST COMPLETE#
   STOP

PROGRAM 65   115 BYTES

   READ PROBE
   REG1 = REGB
1: LABEL 1
   IF REG8 = 0 GOTO 2
   READ @ REG9
   GOTO 3
2: LABEL 2
   WRITE @ REG9 = REGA
3: LABEL 3
   DEC REG1
   IF REG1 > 0 GOTO 1
   READ PROBE
   REG0 = REG0 AND 7F
   IF REG0 = REGB GOTO 4
   DPY-+CNT BAD.
   GOTO 5
4: LABEL 4
   DPY-+ OK. 
5: LABEL 5
   DPY-+COUNT @0=@B#
   STOP

PROGRAM 70

READ 2803
DPY $E
READ 2802
DPY +$E
READ 2801
DPY +$E
READ 2800
DPY +$E
STOP

PROGRAM 71                    !DIP switch test (needs work)

   REGF = 27FF

PROGRAM 80   76 BYTES

   SYNC DATA
2: LABEL 2
   DPY-CONT START.
   DPY-+#
   STOP
   READ PROBE
   EXECUTE PROGRAM 85
   READ PROBE
   REG0 = REG0 SHR SHR SHR SHR
   REG0 = REG0 SHR SHR SHR SHR
   REG0 = REG0 AND FFFF
   DPY-SIG=$0
   DPY-+#
   STOP
   GOTO 2

PROGRAM 85

   WRITE @ 4000 = FF
   WRITE @ 4001 = A3
   WRITE @ 4002 = 00
   WRITE @ 4003 = 02
   WRITE @ 4004 = FF
   WRITE @ 4005 = 97
   WRITE @ 4006 = 00
   WRITE @ 4007 = 90
   WRITE @ 4008 = 00
   WRITE @ 4009 = A2
   WRITE @ 400A = 00
   WRITE @ 400B = 00
   WRITE @ 400C = 00
   WRITE @ 400D = 90
   WRITE @ 400E = FF
   WRITE @ 400F = 33
   WRITE @ 4010 = 00
   WRITE @ 4011 = E0
   WRITE @ 3000 = 00

!!!!!! Pause Routine !!!!!!!!!!!!!!!

PROGRAM PAUSE

REG2 = REG8
	DLOOP:
	DEC REG2
	IF REG2 > 0 GOTO DLOOP
